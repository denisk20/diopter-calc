apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def publisherJson = System.getProperty("user.home") + '/.ssh/endmyopia-play.json'
if(new File(publisherJson).exists()) {
    play {
        jsonFile = file(publisherJson)
    }
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "org.endmyopia.calc"
        minSdkVersion 24
        targetSdkVersion 28
        versionName '0.6'
        versionCode Integer.parseInt(getBuildNumberFile().text)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            def STUB = "stub"
            //these variables should be defined in ~/.gradle/gradle.properties file
            storeFile file(project.hasProperty("EM_RELEASE_STORE_FILE") ? RELEASE_STORE_FILE : STUB);
            storePassword project.hasProperty("EM_RELEASE_STORE_PASSWORD") ? RELEASE_STORE_PASSWORD : STUB;
            keyAlias project.hasProperty("EM_RELEASE_KEY_ALIAS") ? RELEASE_KEY_ALIAS : STUB;
            keyPassword project.hasProperty("EM_RELEASE_KEY_PASSWORD") ? RELEASE_KEY_PASSWORD : STUB;
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    dataBinding {
        enabled = true
    }
}

File getBuildNumberFile() {
    return new File(project.projectDir.path + '/build-number.txt')
}

void incrementBuildNumber() {
    def file = getBuildNumberFile()
    int buildNumber = file.text.toInteger() + 1;
    file.write("" + buildNumber);
}

task incrementBuildNumber {
    doLast {
        incrementBuildNumber()
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.5.0-alpha02'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation 'com.google.android.material:material:1.3.0-alpha02'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'com.google.ar:core:1.19.0'
    implementation "com.google.ar.sceneform.ux:sceneform-ux:1.17.1"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version" // For Kotlin use kapt instead of annotationProcessor
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation "androidx.preference:preference:1.1.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"

    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"
}
